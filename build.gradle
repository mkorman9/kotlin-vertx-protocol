buildscript {
    ext {
        kotlinVersion = '1.5.31'
        grpcVersion = '1.46.0'
        grpcKotlinVersion = '1.3.0'
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.18'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
}

apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'

scmVersion {
    useHighestVersion = true
    versionCreator 'versionWithBranch'
}

group 'com.github.mkorman9.vertx'
version scmVersion.version

def javaVersion = '11'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

if (!project.hasProperty('url')) {
    ext.url = ''
}
if (!project.hasProperty('username')) {
    ext.username = ''
}
if (!project.hasProperty('password')) {
    ext.password = ''
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username project.username
                password project.password
            }

            url project.url
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'

    implementation "io.grpc:grpc-netty:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-kotlin-stub:$grpcKotlinVersion"
    implementation "com.google.protobuf:protobuf-kotlin:3.21.1"
}

sourceSets {
    src {
        main {
            java {
                srcDirs 'build/generated/source/proto/main/grpc'
                srcDirs 'build/generated/source/proto/main/kotlin'
            }
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.1'
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpcKotlinVersion:jdk8@jar"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
            grpckt {}
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
    sourceCompatibility = javaVersion
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
    sourceCompatibility = javaVersion
}
